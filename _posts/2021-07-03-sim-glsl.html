---
title: Simulating worlds on the GPU
subtitle: "Part I: Protoplanet"
image: https://i.vimeocdn.com/video/717923611
abstract: This is the first in a series of posts explaining my <a href="https://www.shadertoy.com/view/XttcWn">procedural earth simulation</a>, written entirely in GLSL fragment shaders.
layout: texify
---

<figure>
<iframe src="https://player.vimeo.com/video/283607168" width="640" height="360" frameborder="0" allow="autoplay; fullscreen; picture-in-picture" allowfullscreen></iframe>
<figcaption>A video recording of the <a href="https://www.shadertoy.com/view/XttcWn">final shader</a>.</figcaption>
</figure>

<h2>Protoplanet</h2>

<blockquote>
<p>This story begins four and a half billion years ago, with a lump of molten rock...
</blockquote>

<figure>
<iframe src="https://www.shadertoy.com/embed/XsGBDt" width="640" height="360"></iframe>
</figure>

<p>The early earth was a <a href="https://en.wikipedia.org/wiki/Protoplanet">protoplanet</a>, red hot and heavily cratered by asteroid impacts. As my earth simulation is <em>entirely procedurally generated</em>, with no pre-rendered textures, the first task is to generate a map of this terrain. To calculate the <code>height</code> of the terrain at a given <code>lat</code>itude and <code>lon</code>gitude, first translate to 3D cartesian coordinates:

<pre><code class="glsl">
vec3 p = 1.5 * vec3(
    sin(lon*PI/180.) * cos(lat*PI/180.),
    sin(lat*PI/180.),
    cos(lon*PI/180.) * cos(lat*PI/180.));
</code></pre>

<p>Now, as asteroids come in a variety of sizes, so do the resulting craters. To accommodate this, the shader iterates over five levels of detail, layering craters of decreasing size over each other. To make the craters have a realistic rugged appearance, this is mixed with some <a href="https://iquilezles.untergrund.net/www/articles/fbm/fbm.htm">fractional Brownian motion</a> noise, and scaled so that the largest craters have the most impact on the terrain.

<pre><code class="glsl">
float height = 0.;
for (float i = 0.; i &lt; 5.; i++) {
    float c = craters(0.4 * pow(2.2, i) * p);
    float noise = 0.4 * exp(-3. * c) * FBM(10. * p);
    float w = clamp(3. * pow(0.4, i), 0., 1.);
    height += w * (c + noise);
}
height = pow(height, 3.);
</code></pre>

<p>The craters themselves are generated on a 3D grid, from which a sphere is carved out for the surface terrain. To avoid visible regularity, the crater centres are given a pseudo-random offset from the grid points, using a <a href="https://www.shadertoy.com/view/4djSRW">hash function</a>. To calculate influence of a crater at a given location, take a weighted average of the craters belonging to the nearby grid points, with weights exponentially decreasing with distance from the centre. The crater rims are generated by a simple sine curve.

<pre><code class="glsl">
float craters(vec3 x) {
    vec3 p = floor(x);
    vec3 f = fract(x);
    float va = 0.;
    float wt = 0.;
    for (int i = -2; i &lt;= 2; i++)
     for (int j = -2; j &lt;= 2; j++)
      for (int k = -2; k &lt;= 2; k++) {
        vec3 g = vec3(i,j,k);
        vec3 o = 0.8 * hash33(p + g);
        float d = distance(f - g, o);
        float w = exp(-4. * d);
        va += w * sin(2.*PI * sqrt(d));
        wt += w;
    }
    return abs(va / wt);
}
</code></pre>

<p>The final procedurally generated heightmap looks like this:

<figure>
<img src="/img/protoplanet.jpg">
</figure>

<p>Although relatively simple, this procedural terrain resembles what scientists believe the early earth actually looked like:

<figure>
<img src="https://sservi.nasa.gov/wp-content/uploads/2014/07/7-30-14_hadeanearth.jpg">
<figcaption>Artistic impression of the early earth, by <a href="https://sservi.nasa.gov/articles/new-nasa-research-shows-giant-asteroids-battered-early-earth/">NASA</a>.</figcaption>
</figure>

<h2>Ancient Ocean</h2>

<blockquote>
<p>Water contained within was vaporised by the heat, which escaped and began circulating through the early atmosphere forming around the planet. As time progressed and the rock cooled, the water vapour began to condense into oceans. The flow of liquid water across the surface carved valleys in the terrain, leaving an accumulation of sediment in its wake.
</blockquote>

<p>Stay tuned for the next post in the series, which introduces a global atmosphere and water flow simulation.
